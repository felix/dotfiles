#!/usr/bin/awk -f

function ltrim(s) { sub(/^[ \t\r\n]+/, "", s); return s }
function rtrim(s) { sub(/[ \t\r\n]+$/, "", s); return s }
function trim(s) { return rtrim(ltrim(s)); }

BEGIN{
	if (ARGV[1] != "get") {
		exit 0
	}
	ARGC--
	FS="="
	getline
}
$1 == "host" {
	pass = "pass show "$2
	username = ENVIRON["USER"]
	FS=":"
	while ((pass | getline) > 0) {
		if ($1 ~ "login|user") {
			username = trim($2)
		}
		if ($1 == "pat") {
			printf "password=%s\nusername=%s\n", trim($2), username
			exit 0
		}
	}
	close(pass)
	exit 0
}
